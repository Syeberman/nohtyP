import os.path

# Confirm that scons' top-level directory, the current directory, _is_ the project top
if not os.path.exists( "nohtyP.c" ):
    Exit( "Run SCons from top project dir (or use make.bat, or scons -C)" )

# Command-line build variables
vars = Variables( None )

# Generic build environment applicable to all compilers/targets and used internally by SCons
rootEnv = DefaultEnvironment( tools=[], variables=vars )

# Require that only known command-line variables are used
vars_unknown = vars.UnknownVariables( )
if vars_unknown: Exit( "Unknown variables: %r" % vars_unknown.keys( ) )

# Require that construction variable names exist at expansion, then add those allowed to be empty
AllowSubstExceptions( )
rootEnv.Append( CPPFLAGS="", SHCCCOMSTR="", SHLINKCOMSTR="", SHLIBVERSION="", LIBPATH="", PCH="", )

# Put .sconsign.dblite (et al) in Build rather than the top nohtyP directory
rootEnv.SConsignFile( os.path.abspath( "Build/.sconsign" ) )

# For dependencies, first consider timestamps, then MD5 checksums
rootEnv.Decider( "MD5-timestamp" )

# Always use cmd.exe on Windows, regardless of user's shell; ignored on other platforms
rootEnv["ENV"]["COMSPEC"] = "cmd.exe"


# TODO Support more compilers, control from command-line
env = rootEnv.Clone( tools=["msvs_100", ], toolpath=["#Build/Tools", ] )
env["OBJDIR"] = "#Build/${yp_COMPILER}/${PLATFORM}_${TARGET_ARCH}_Release"

# Maintain a separate object file directory, and don't copy source files there
env.VariantDir( "$OBJDIR", "#", duplicate=False )

# How to build nohtyP.dll
# TODO Implement as an SConscript?
env.PrependUnique(
        CPPPATH=[
            "#",
            ],
        CPPDEFINES=[
            "yp_ENABLE_SHARED",
            "yp_BUILD_CORE",
            ],
        PDB="", LIBS="",
        )

env.SharedLibrary( "$OBJDIR/nohtyP", source="$OBJDIR/nohtyP.c" )
# TODO Remove the target directory entirely?

